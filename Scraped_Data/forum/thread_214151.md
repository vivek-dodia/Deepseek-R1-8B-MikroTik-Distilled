---
title: Untitled Thread
source_url: https://forum.mikrotik.com/viewtopic.php?f=2&t=214151&p=1122718&amp;sid=3b77a3334c914448dbbc02bfdff4c3aa#p1122718
crawled_date: 2025-02-03T11:36:28.327460
section: mikrotik_forum
type: forum_thread
---

### Author: Fri Jan 24, 2025 10:18 am
Date: Unknown

Hi,I have 2 global IPV6 prefixes in my network and my goal is to use netwatch to update the distance of the two gateway and also "gently" disable the prefix thats down. I wanted to use the command /ipv6 advertise=no but that is not recognized.What is a good way to achieve that? note: i do not want to use NPT or NAT for IPv6, i want to use the full delegated network as it is actually intended by the IPv6 standard, so that the clients will select the best route/prefix themselves over SLAAC.the ressources on this topic are very limited as it seems, everybody wants to mess around with NAT for IPv6, which is just wrong.


---
### Author: Thu Jan 30, 2025 12:23 am
Date: Unknown

seems like ROS cant do that. But I have set the ra-lifetime on /ipv6 nd to 1 minute (and 5s ra-interval) and a netwatch script is actually removing the ipv6 address from the prefix pool thats down. That seems to do the trick for now. Even though its not a nice solution.


---
### Author: [SOLVED]Thu Jan 30, 2025 11:26 am
Date: Unknown

```
Code:Select all# Set interface names
/interface set [find default-name=ether2] name=WAN1
/interface set [find default-name=ether3] name=WAN2

[.... VLAN and DHCP configuration and firewall-rules for fasttrack ... ]

# DNS setup
/ip dns set servers=9.9.9.9,1.1.1.2,2620:fe::9,2606:4700:4700::1112 allow-remote-requests=yes

# WAN DHCP clients (IPv4 and IPv6)
/ip dhcp-client add interface=WAN1 add-default-route=no disabled=no
/ip dhcp-client add interface=WAN2 add-default-route=no disabled=no
/ipv6 dhcp-client add interface=WAN1 add-default-route=no pool-name=WAN1-pool prefix-hint=::/62 request=address,prefix disabled=no use-peer-dns=no
/ipv6 dhcp-client add interface=WAN2 add-default-route=no pool-name=WAN2-pool prefix-hint=::/62 request=address,prefix disabled=no use-peer-dns=no

# IPv6 Address assignment for VLAN50
/ipv6 address add address=fd37:443d:252b:0::1/64 interface=main-network advertise=no comment="ULA generated for this interface as DNSv6 address for RDNSS"
/ipv6 address add from-pool=WAN1-pool interface=main-network address=::1 advertise=yes comment="Prefix from WAN1"
/ipv6 address add from-pool=WAN2-pool interface=main-network address=::2 advertise=yes comment="Prefix from WAN2"


# ipv4 default routes and special routes for online-check
/ip route add dst-address=0.0.0.0/0 distance=1 gateway=192.168.1.1 check-gateway=ping comment="Primary WAN"
/ip route add dst-address=0.0.0.0/0 distance=2 gateway=192.168.2.1 check-gateway=ping comment="Secondary WAN"
/ip route add dst-address=8.8.8.8/32 gateway=192.168.1.1 distance=1 comment="Primary WAN monitoring ipv4"
/ip route add dst-address=1.1.1.1/32 gateway=192.168.2.1 distance=1 comment="Secondary WAN monitoring ipv4"
/ip route add dst-address=8.8.8.8/32 blackhole distance=10 comment="Primary WAN monitoring ipv4"
/ip route add dst-address=1.1.1.1/32 blackhole distance=10 comment="Secondary WAN monitoring ipv4"

# ipv6 default routes and special routes for online-check
/ipv6 route add dst-address=::/0 distance=1 gateway=fe80::%WAN1 check-gateway=ping comment="Primary IPv6 WAN"
/ipv6 route add dst-address=::/0 distance=2 gateway=fe80::%WAN2 check-gateway=ping comment="Secondary IPv6 WAN"
/ipv6 route add dst-address=2001:4860:4860::8888/128 gateway=fe80::%WAN1 distance=1 comment="Primary WAN monitoring ipv6"
/ipv6 route add dst-address=2606:4700:4700::1111/128 gateway=fe80::%WAN2 distance=1 comment="Secondary WAN monitoring ipv6"
/ipv6 route add dst-address=2001:4860:4860::8888/128 blackhole distance=10 comment="Primary WAN monitoring ipv6"
/ipv6 route add dst-address=2606:4700:4700::1111/128 blackhole distance=10 comment="Secondary WAN monitoring ipv6"




# Failover-Script
/system script add name="WAN1-up-4" dont-require-permissions=yes source=":local MyRoute [/ip route find where dst-address=0.0.0.0/0 gateway=192.168.1.1]; /ip route set \$MyRoute distance=1; /log warning \"IPv4 failover event: WAN1 IPv4 is now UP"
/system script add name="WAN2-up-4" dont-require-permissions=yes source=":local MyRoute [/ip route find where dst-address=0.0.0.0/0 gateway=192.168.2.1]; /ip route set \$MyRoute distance=2; /log warning \"IPv4 failover event: WAN2 IPv4 is now UP"
/system script add name="WAN1-down-4" dont-require-permissions=yes source=":local MyRoute [/ip route find where dst-address=0.0.0.0/0 gateway=192.168.1.1]; /ip route set \$MyRoute distance=10; /log warning \"IPv4 failover event: WAN1 IPv4 is now DOWN"
/system script add name="WAN2-down-4" dont-require-permissions=yes source=":local MyRoute [/ip route find where dst-address=0.0.0.0/0 gateway=192.168.1.1]; /ip route set \$MyRoute distance=11; /log warning \"IPv4 failover event: WAN2 IPv4 is now DOWN"
/tool netwatch add host=8.8.8.8 interval=5s timeout=2s up-script="/system script run WAN1-up-4" down-script="/system script run WAN1-down-4"
/tool netwatch add host=1.1.1.1 interval=5s timeout=2s up-script="/system script run WAN2-up-4" down-script="/system script run WAN2-down-4"
/system script add name="WAN1-up-6" dont-require-permissions=yes source=":local MyRoute [/ipv6 route find where dst-address=::/0 gateway=fe80::%WAN1]; /ipv6 route set \$MyRoute distance=1; /ipv6 address add from-pool=WAN1-pool interface=main-network address=::1 advertise=yes comment= \"Prefix from WAN1\"; /log warning \"IPv6 failover event: WAN1 IPv6 is now UP"
/system script add name="WAN2-up-6" dont-require-permissions=yes source=":local MyRoute [/ipv6 route find where dst-address=::/0 gateway=fe80::%WAN2]; /ipv6 route set \$MyRoute distance=2; /ipv6 address add from-pool=WAN2-pool interface=main-network address=::2 advertise=yes comment=\"Prefix from WAN2\"; /log warning \"IPv6 failover event: WAN2 IPv6 is now UP"
/system script add name="WAN1-down-6" dont-require-permissions=yes source=":local MyRoute [/ipv6 route find where dst-address=::/0 gateway=fe80::%WAN1]; /ipv6 route set \$MyRoute distance=10; /ipv6 address remove [find where from-pool=WAN1-pool]; /log warning \"IPv6 failover event: WAN1 IPv6 is now DOWN"
/system script add name="WAN2-down-6" dont-require-permissions=yes source=":local MyRoute [/ipv6 route find where dst-address=::/0 gateway=fe80::%WAN2]; /ipv6 route set \$MyRoute distance=11; /ipv6 address remove [find where from-pool=WAN2-pool] /log warning \"IPv6 failover event: WAN2 IPv6 is now DOWN"
/tool netwatch add host=2001:4860:4860::8888 interval=5s timeout=2s up-script="/system script run WAN1-up-6" down-script="/system script run WAN1-down-6"
/tool netwatch add host=2606:4700:4700::1111 interval=5s timeout=2s up-script="/system script run WAN2-up-6" down-script="/system script run WAN2-down-6"


# IPv6 Router Advertisement configuration
/ipv6 nd add interface=main-network ra-lifetime=1m ra-delay=5s ra-interval=5s-10s other-configuration=yes dns=fd37:443d:252b:0::1
/ipv6 nd add interface=all disabled=yes
```

As Im the first one attempting this (I cannot find any other postings in the forum with a successful working IPv6 failover config) I will post my current setup, which works flawlessly as of now. Note: Im using 8.8.8.8 and 1.1.1.1 as monitoring IP, but Im not using them for actual DNS, Im using quad9 and 1.1.1.2 for that (maleware block). Important: for check-gateway and netwatch to work, those IPs must be unreachable during WAN-failover. Blackhole routes are the only means to do that. There are scripts in the forum using recursive routing and route-marking, thats incorrect and does not reliably work. Blackholing is actually meant to be used for this.

